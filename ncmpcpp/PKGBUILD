# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.6.6
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='http://ncmpcpp.rybczak.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(${pkgname}-${pkgver}.tar.bz2::http://ncmpcpp.rybczak.net/stable/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('5f288c1e5b96b38586e6c3230b09380584e902bc5d44147433093c48e4b61efd32b3b405ee96b4341e56ab2b26590ed2320bf1ca188e46796c13be5da52acb58')

build() {
  cd ${pkgname}-${pkgver}
  BOOST_LIB_SUFFIX='' ./configure --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
