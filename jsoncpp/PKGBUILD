# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

pkgname=jsoncpp
pkgver=1.7.7
pkgrel=1
pkgdesc='C++ library for interacting with JSON'
url='https://github.com/open-source-parsers/jsoncpp'
arch=('i686' 'x86_64')
license=('MIT' 'custom:Public_Domain')
depends=('gcc-libs')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/open-source-parsers/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('1b12cc497729ca447dae77f92c6a761494554021b306e8cedbea0ddf83f6569ca53380f8b53a0d4a62b5239c605328a419082e0610ca051a2957bcb7136ba644')

build() {
  cd ${pkgname}-${pkgver}
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=OFF
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd  ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

