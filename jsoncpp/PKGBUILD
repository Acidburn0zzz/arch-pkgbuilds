# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

pkgname=jsoncpp
pkgver=1.8.0
pkgrel=1
pkgdesc='C++ library for interacting with JSON'
url='https://github.com/open-source-parsers/jsoncpp'
arch=('i686' 'x86_64')
license=('MIT' 'custom:Public_Domain')
depends=('gcc-libs')
makedepends=('cmake')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/open-source-parsers/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('bd0aa56827932e5b50231216c9f501da12053bf9eed6e83210b5c52afb9aff610e71995446a7e2f5f4580ff37762956a307867d3eaa1aebd6ca31e574c9e4d4c')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=ON \
    ..
  make
}

check() {
  cd ${pkgname}-${pkgver}/build
  make test
}

package() {
  cd  ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
