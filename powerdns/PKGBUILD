# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

pkgname=powerdns
pkgver=4.0.1
pkgrel=1
pkgdesc='Authoritative DNS server'
url='http://www.powerdns.com/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('boost-libs' 'libsodium' 'libsystemd' 'lua' 'openssl' 'protobuf' 'sqlite')
makedepends=('boost' 'setconf' 'libmariadbclient' 'postgresql-libs' 'lua' 'libldap'
             'sqlite' 'geoip' 'yaml-cpp' 'pkg-config')
optdepends=('libmariadbclient: MariaDB/MySQL backend'
            'postgresql-libs: PostgreSQL backend'
            'libldap: LDAP backend'
            'geoip: GeoIP backend'
            'yaml-cpp: GeoIP backend')
install="${pkgname}.install"
backup=('etc/powerdns/pdns.conf')
source=(http://downloads.powerdns.com/releases/pdns-${pkgver}.tar.bz2{,.asc})
sha512sums=('77fce9963a05198afeb569f92fbb0f6a1cb3426c28dd77b0921128189c80d9a72ebdbfc249dfc0b5b89cc7a65a83887a0388d6cc3461453b1e3096e563afdd1e'
            'SKIP')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7') # Pieter Lexis <pieter.lexis@powerdns.com>

prepare() {
  cd pdns-${pkgver}

  # Default settings
  for keyvalue in \
    allow-recursion=127.0.0.1 \
    cache-ttl=6 \
    chroot=/var/empty \
    config-dir=/etc \
    daemon=yes \
    distributor-threads=3 \
    guardian=yes \
    local-port=53 \
    loglevel=3 \
    module-dir=/usr/lib/powerdns \
    negquery-cache-ttl=60 \
    setgid=nobody \
    setuid=nobody \
    socket-dir=/var/run \
    webserver=no
  do
    setconf -a pdns.conf "${keyvalue}"
  done

  # Using simple so we can bind it to a chroot
  sed 's|notify|simple|' -i pdns/pdns.service.in
}

build() {
  cd pdns-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --with-modules='' \
    --with-dynmodules="bind gmysql geoip gpgsql gsqlite3 ldap lua pipe random remote" \
    --docdir=/usr/share/doc/powerdns \
    --sbindir=/usr/bin \
    --bindir=/usr/bin \
    --with-sqlite3 \
    --enable-libsodium \
    --enable-tools \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-unit-tests
  make
}

check() {
  make -C pdns-${pkgver} check
}

package() {
  cd pdns-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 pdns.conf -t "${pkgdir}/etc/powerdns"
  mv "${pkgdir}/usr/lib/pdns" "${pkgdir}/usr/lib/powerdns"
}

# vim: ts=2 sw=2 et:
