# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=libpsl
pkgver=0.14.0
pkgrel=2
pkgdesc='Publix Suffic List library'
url='https://github.com/rockdaboot/libpsl'
arch=('i686' 'x86_64')
license=('MIT')
depends=('icu>=55.1-1')
makedepends=('libxslt' 'python2' 'publicsuffix-list')
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('221b913706cb556836a6deb6271daea69f94e18437dc6f603f5c3c10738cf2bd07b47b5192b710e70c0e371db1372e582298eb17d6a907606743e58af2ff8b98')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's/python$/python2/g' -i src/psl-make-dafsa
  rm -frv list
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-dependency-tracking \
    --enable-man \
    --enable-builtin=libicu \
    --with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/libpsl/libpsl.3 -t "${pkgdir}/usr/share/man/man3"
}

# vim: ts=2 sw=2 et:
