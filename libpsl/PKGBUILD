# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=libpsl
pkgver=0.16.1
pkgrel=1
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=('i686' 'x86_64')
license=('MIT')
depends=('icu>=55.1-1')
makedepends=('libxslt' 'python' 'publicsuffix-list')
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('409c06dd3661ef07cfcea4402454d55609c7448032d074cf10795809745b0130e7954f29c9fea5a3964a749f3ec4bcf20cf10cfd86d332fb8ddc72302fe4ba80')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -frv list
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-dependency-tracking \
    --enable-man \
    --enable-builtin=libicu \
    --enable-runtime=libicu \
    --with-psl-file=/usr/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/usr/share/publicsuffix/test_psl.txt
  LC_CTYPE=en_US.UTF-8 make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/libpsl/libpsl.3 -t "${pkgdir}/usr/share/man/man3"
}

# vim: ts=2 sw=2 et:
