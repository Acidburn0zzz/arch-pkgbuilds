# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=hexer
pkgver=0.2.1
pkgrel=1
pkgdesc="A multi buffer editor for binary files with vi-like interface"
url="http://devel.ringlet.net/"
arch=('i686' 'x86_64')
license=('custom')
depends=('ncurses')
source=(http://devel.ringlet.net/editors/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('5994b9daec12e6e141c434e6c620ee6628602bc0b82e717b296d3a2ea4bfb62faa791f82e9052a093844ec402542528b7ee6ef154d3b80cd477d2b3387434f15'
            'SKIP')
validpgpkeys=('2EE7A7A517FC124CF115C354651EEFB02527DF13') # Peter Pentchev <roam@ringlet.net>

prepare() {
  cd ${pkgname}-${pkgver}
  cp config.{linux,h}
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr MANDIR=/usr/share/man/man1 install
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
