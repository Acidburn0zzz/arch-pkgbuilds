# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=john
pkgver=1.8.0
pkgrel=1
_jumbover=1
pkgdesc="John The Ripper - A fast password cracker (jumbo-$_jumbover included)"
arch=('i686' 'x86_64')
url="http://www.openwall.com/john"
license=('GPL2' 'custom')
depends=('openssl' 'gmp')
optdepends=("perl: for executing some of the scripts at /usr/share/john"
            "ruby: for executing some of the scripts at /usr/share/john"
            "python: for executing some of the scripts at /usr/share/john")
backup=('etc/john/john.conf')
install=john.install
source=(http://www.openwall.com/john/j/john-$pkgver-jumbo-$_jumbover.tar.xz
        params.h.patch
        allow-cflags-overriding.patch
        gcc5.patch)
sha512sums=('163cd71f634c2d1e9d0fa760984cc05001bfeef8300098d6b9cc8bf7e1719fec1d37142c39d7fd65ef37ee96c95681f01d7f0b1941058b7f9926442e2df5cd8e'
            '64406b70aaad0a4743b029fe30ceb80c3963dadd83ffe2c732cba8abefa52304ea163a51fa3aee667bfae50bdf4f49dc4e2941fce372248de8b2f10e434bd8a7'
            '9c6405fb14e52750a554bbc9b350baa0af44d01ff8718c50b58058a616b72cb839c6e0ca9f1f36c35aa7976f3d5ff77bdd583e3588284a13618e4d81952916a4'
            '941a8fbe435d24287be158648d3083062806de639ce822645d949a171b2e662249bb13d9f5903017792cc979c7ff89615681adaed2afbf0d9c6ca5fe825e135a')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver-jumbo-$_jumbover/src
  patch -p0 < "${srcdir}/params.h.patch"
  patch -p1 < "${srcdir}/allow-cflags-overriding.patch"
  patch -p2 < "${srcdir}/gcc5.patch"
}

build() {
  cd ${srcdir}/$pkgname-$pkgver-jumbo-$_jumbover/src
  ./configure --prefix=/usr
  CFLAGS="${CFLAGS} -DJOHN_SYSTEMWIDE" make
}

package() {
  subdir="$pkgname-$pkgver-jumbo-$_jumbover"

  # config file
  sed -i 's|$JOHN/john.local.conf|/etc/john/john.local.conf|g' ${srcdir}/${subdir}/run/john.conf
  sed -i 's|$JOHN|/usr/share/john|g' ${srcdir}/${subdir}/run/john.conf
  install -Dm644 ${srcdir}/${subdir}/run/john.conf ${pkgdir}/etc/john/john.conf

  # docs
  install -d ${pkgdir}/usr/share/doc/john
  install -m644 ${srcdir}/${subdir}/doc/* ${pkgdir}/usr/share/doc/john/
  install -Dm644 ${srcdir}/${subdir}/doc/LICENSE ${pkgdir}/usr/share/licensesa/$pkgname/LICENSE

  # install password lists, charsets, other files and scripts
  install -d ${pkgdir}/usr/share/john/

  # install scripts (m755)
  john_scripts=(1password2john.py       7z2john.py          aix2john.pl   \
                aix2john.py             androidfde2john.py  apex2john.py  \
                benchmark-unify         bitcoin2john.py     blockchain2john.py \
                cisco2john.pl           cracf2john.py       dmg2john.py   \
                ecryptfs2john.py        efs2john.py         encfs2john.py \
                genincstats.rb          hextoraw.pl         htdigest2john.py \
                ikescan2john.py         ios7tojohn.pl       kdcdump2john.py  \
                keychain2john.py        keystore2john.py    known_hosts2john.py \
                kwallet2john.py         ldif2john.pl        leet.pl       \
                lion2john-alt.pl        lion2john.pl        lotus2john.py \
                mailer                  mcafee_epo2john.py  ml2john.py    \
                mozilla2john.py          netntlm.pl          netscreen.py  \
                odf2john.py             office2john.py      openbsd_softraid2john.py \
                openssl2john.py         pass_gen.pl         pcap2john.py  \
                pdf2john.py             radius2john.pl      rexgen2rules.pl \
                sap2john.pl             sha-dump.pl         sha-test.pl   \
                sipdump2john.py         sshng2john.py       strip2john.py \
                sxc2john.py             mailer)

  # install non-executable files (m644)
  john_share=(alnum.chr               alnumspace.chr      alpha.chr     \
              ascii.chr               digits.chr          lanman.chr    \
              latin1.chr              lm_ascii.chr        lower.chr     \
              lowernum.chr            lowerspace.chr      upper.chr     \
              uppernum.chr            utf8.chr                          \
              dumb16.conf             dumb32.conf         dynamic.conf  \
              dynamic_flat_sse_formats.conf               korelogic.conf \
              regex_alphabets.conf    repeats16.conf      repeats32.conf \
              password.lst            dictionary.rfc2865  stats)
  for john_script in "${john_scripts[@]}"; do
    install -m755 ${srcdir}/${subdir}/run/${john_script} \
      ${pkgdir}/usr/share/john
  done
  for share in "${john_share[@]}"; do
    install -m644 ${srcdir}/${subdir}/run/${share} \
      ${pkgdir}/usr/share/john/
  done

  install -m644 ${srcdir}/${subdir}/run/dynamic.conf ${pkgdir}/etc/john/
  install -Dm644 ${srcdir}/${subdir}/run/john.bash_completion \
    ${pkgdir}/etc/bash_completion.d/john
  install -Dm644 ${srcdir}/${subdir}/run/john.zsh_completion \
    ${pkgdir}/usr/share/zsh/site-functions/_john

  # install binaries
  install -Dm755 ${srcdir}/${subdir}/run/john ${pkgdir}/usr/bin/john
  install -Dm755 ${srcdir}/${subdir}/run/calc_stat ${pkgdir}/usr/bin/calc_stat
  install -Dm755 ${srcdir}/${subdir}/run/cprepair ${pkgdir}/usr/bin/cprepair
  install -Dm755 ${srcdir}/${subdir}/run/genmkvpwd ${pkgdir}/usr/bin/genmkvpwd
  install -Dm755 ${srcdir}/${subdir}/run/luks2john ${pkgdir}/usr/bin/luks2john
  install -Dm755 ${srcdir}/${subdir}/run/mkvcalcproba ${pkgdir}/usr/bin/mkvcalcproba
  install -Dm755 ${srcdir}/${subdir}/run/raw2dyna ${pkgdir}/usr/bin/raw2dyna
  install -Dm755 ${srcdir}/${subdir}/run/relbench ${pkgdir}/usr/bin/relbench
  install -Dm755 ${srcdir}/${subdir}/run/tgtsnarf ${pkgdir}/usr/bin/tgtsnarf
  install -Dm755 ${srcdir}/${subdir}/run/uaf2john ${pkgdir}/usr/bin/uaf2john
  install -Dm755 ${srcdir}/${subdir}/run/wpapcap2john ${pkgdir}/usr/bin/wpapcap2john

  # create links
  cd ${pkgdir}/usr/bin
  john_links=(base64conv      dmg2john        gpg2john      hccap2john    \
              keepass2john    keychain2john   keyring2john  keystore2john \
              kwallet2john    pfx2john        putty2john    pwsafe2john   \
              racf2john       rar2john        ssh2john      unique        \
              truecrypt_volume2john           unafs         undrop        \
              unshadow        zip2john)
  for link in "${john_links[@]}"; do
    ln -s john $link
  done
}

# vim: ts=2 sw=2 et:
