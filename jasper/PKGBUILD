# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=jasper
pkgver=1.900.30
pkgrel=1
pkgdesc='Software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard'
url='http://www.ece.uvic.ca/~mdadams/jasper/'
arch=('i686' 'x86_64')
license=('custom:JasPer2.0')
depends=('libjpeg')
makedepends=('freeglut' 'libxmu' 'glu')
optdepends=('freeglut: jiv support'
            'glu: jiv support')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mdadams/jasper/archive/version-${pkgver}.tar.gz
        jasper-1.900.1-fix-filename-buffer-overflow.patch)
sha512sums=('887cb6f4167bd8aedb067851e064a550b1d3c5247204d8cba4d87e47f2fc42b8f7d64ab62be74838b452a821f9e467665f44dadedc84b268510159ad1de68311'
            'b8d798bf75523c5db263783e42c653dd0cb03deee90be32eddf878bb6893cca02abadd94de6a8c737a5b7fe76f7fb245979f010765e6a95fc520b215e3a2a7f0')

prepare() {
  cd ${pkgname}-version-${pkgver}
  patch -p1 < "${srcdir}/jasper-1.900.1-fix-filename-buffer-overflow.patch"
  autoreconf -fvi
}

build() {
  cd ${pkgname}-version-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared
  make
}

package() {
  cd ${pkgname}-version-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
