# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=mtr
pkgname=('mtr' 'mtr-gtk')
pkgver=0.87
pkgrel=1
pkgdesc='Combines the functionality of traceroute and ping into one tool'
url='http://www.bitwizard.nl/mtr/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('ncurses' 'gtk2' 'libcap')
install=mtr.install
source=(${pkgbase}-${pkgver}.tar.gz::ftp://ftp.bitwizard.nl/mtr/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('172ba2f78a6ee4deb427ac2fb00dff16843215dbd23663fbe41fcc957ffe278b408a73a0c7e1c05788b235e2d03fb53f9971b8b82d4de2f95edcaa9f2cff3cf9')

prepare() {
  (cd ${pkgbase}-${pkgver}
    autoreconf -fiv
  )
  cp -ra ${pkgbase}-${pkgver}{,-cli}
}

build() {
  (cd ${pkgbase}-${pkgver}-cli
    ./configure --prefix=/usr --without-gtk --sbindir=/usr/bin
    make
  )
  (cd ${pkgbase}-${pkgver}
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
  )
}

package_mtr() {
  pkgdesc='Combines the functionality of traceroute and ping into one tool (CLI version)'
  depends=('ncurses' 'libcap')
  cd ${pkgbase}-${pkgver}-cli
  make DESTDIR="${pkgdir}" install
}

package_mtr-gtk() {
  pkgdesc='Combines the functionality of traceroute and ping into one tool (GTK version)'
  depends=('ncurses' 'libcap' 'gtk2')
  conflicts=('mtr')
  provides=("mtr=${pkgver}")
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
